extern void object::Kill()
{
	int alienType = AlienAnt;
	
	Bot me(this);
	me.hoverHeight = 10;
	me.rechargeLevel = 0.35;
	
	object item;
	
	aim(-20);
	
	while (true)
	{
		//try and find one a little ways away first
		item = radar(alienType, 0, 360, 40);
		if (item == null) item = radar(alienType);
		if (item == null) break;
		
		while (item.isAlive()) {
			me.follow(item, 32, 100);
			motor(1,1);
			if (item != null) {
				turn(direction(item.position));
				//turn(-10);
				
				//calculate the point to go to if we miss
				point p = alongDirection(item.position, orientation, 40);
				
				//find out which way the ant is facing (and is therefore likely to walk);
				float face = item.orientation - orientation;
				while (face > 180) face -= 360;
				while (face < -180) face += 360;
				
				if (face > 0) {
					motor(1 - (90 - abs(face - 90)) / 90 * 0.3, 1);
				} else {
					motor(1, 1 - (90 - abs(face + 90)) / 90 * 0.3);
				}
				
				fire(0.5);
				wait(0.5); //wait for bullets to get there
				
				if (item.isAlive()) {
					message("Missed");
					
					//goto spot to set up next strafe
					me.flyto(p, 2, false);
				}
			}
		}
		wait(0.1);
	}
	
	message("All aliens killed!");
	
	//get landing spot
	point p = alongDirection(position, orientation, 25);
	
	me.bot_goto(p, 3);
}
