extern public void BotClass() {}

public bool object::isAlive() {
	return search(category, position) == this;
}

public point alongDirection(point origin, float angle, float dis) {
	point p;
	p.x = origin.x + cos(angle) * dis;
	p.y = origin.y + sin(angle) * dis;
	return p;
}

public class Bot {
	//States
	int NORMAL = 0;
	int RECHARGING = 1;
	
	object me;
	int state;
	int stuckCount = 0;
	point lastPos;
	float hoverHeight = 10;
	
	public void Bot (object _me) {
		me = _me;
		lastPos = me.position;
		state = NORMAL;
	}
	
	public bool isFlying() {
		return me.category == WingedGrabber ||
		  me.category == WingedShooter ||
		  me.category == WingedSniffer ||
		  me.category == WingedOrgaShooter;
	}
	
	public bool get(int category, float minDistance, bool shouldWait) {
		object o;
		
		while (true) {
			o = radar(category, 0, 360, minDistance);
			if (o != null) break;
			if (!shouldWait) return false;
			wait(0.25);
		};
		
		bot_goto(o.position, 2);
		grab();
		return true;
	}
	
	public void deposit(int flag) {
		if (me.load == null) {
			message("Nothing to deposit!");
			return;
		}
		
		//find a free spot
		object o = radar(flag);
		point free = space(o.position, 0, 20, 2);
		bot_goto(free, 1);
		drop();
	}
	
	public void bot_goto(point pos, float tol) {
		if (isFlying()) {
			flyto(pos, tol, true);
		} else {
			checkPower();
			goto(pos);
		}
	}
	
	public void flyto(point pos, float tollerance, bool land) {
		while(distance2d(me.position, pos) > tollerance) {
			checkPower();
			moveTo(pos, land);
			wait(0.1);
		}
		
		motor(0,0);
	}
	
	public void follow(object target, float dis) {
		
		while(target != null && (distance(me.position, target.position) > dis ||
		    abs(direction(target.position)) > 5)) {
			checkPower();
			moveTo(target.position, false);
			wait(0.1);
		}
		
		motor(0,0);
	}
	
	void checkPower() {
		//check for out of power
		if (state != RECHARGING && me.energyCell.energyLevel < 0.25) {
			message("Recharging...");
			bot_recharge();
			message("Recharging done.");
		}
	}
	
	public void moveTo(point pos, bool land) {
		
		//check for jet overheat
		if (me.temperature > 0.8) {
			message("Cooldown...");
			motor(0,0);
			jet(-1);
			while(me.temperature > 0) {
				wait(0.1);
			}
			message("Cooldown done.");
		}
		
		
		//check for stuck
		if (distance(me.position, lastPos) < 0.25) {
			if (stuckCount++ >= 10) {
				message("stuck!");
				
				//try moving around it
				jet(1);
				motor(-1,-1);
				wait(1);
				turn(90);
				motor(1,1);
				wait(1);
				return;
			}
		} else {
			stuckCount = 0;
			lastPos = me.position;
		}
		
		bool flying = me.altitude > 0;
		
		//Set motors
		point motors;
		motors = turnTowards(pos);
		
		float d = distance2d(me.position, pos);
		
		if (land) {
			if (d > 1) {
				float moveMult = 0.2;
				if (flying) moveMult = 0.05;
				motors.x += d * moveMult;
				motors.y += d * moveMult;
			} else {
				motor(0,0);
				return;
			}
		} else { //if not landing, don't stop, fly through the point
			motors.x += 1;
			motors.y += 1;
		}
		
		motor(motors.x, motors.y);
		
		//Jet
		
		//check terain height
		float alt = topo(me.position);
		if (alt < 0) alt = 0;
		
		float fly = alt - me.position.z;
		if (!land) {
			fly += hoverHeight;
		} else if (d > 5 && d < 15) {
			fly += (d - 5) * (hoverHeight / 10) ;
		} else if (d > 15) {
			fly += hoverHeight;
		} else { // d < 5
			fly = -10;
		}
		
		//message("fly" + fly);
		
		jet(fly * 0.3);
	}
	
	
	public point turnTowards(point pos) {
		float dir = direction(pos);
		float strength = dir / 45;
		point motors(strength * -1, strength);
		return motors;
	}
	
	void bot_recharge() {
		state = RECHARGING;
		
		object o = radar(PowerStation);
		bot_goto(o.position, 1);
		while(me.energyCell.energyLevel < 1) {
			wait(1);
		}
		
		state = NORMAL;
	}
}